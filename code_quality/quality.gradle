plugins.apply('pmd')

check.dependsOn 'pmd'

pmd {
    toolVersion = '5.4.3'
}

task pmd(type: Pmd) {
    ignoreFailures = false // Fail early.
    ruleSetFiles = files("${project.rootDir}/code_quality/pmd.xml")
    ruleSets = []

    source = fileTree('src/main/java')
}


plugins.apply('findbugs')

check.dependsOn 'findbugs'

task findbugs(type: FindBugs) {
    ignoreFailures = false // Fail early.
    effort = 'max'
    reportLevel = 'low' // Report even low priority problems.

    reports {
        html.enabled = true
        xml.enabled = false
    }

    classes = files("${project.projectDir}/build/intermediates/classes")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    // If somebody has an idea how to make this work with support libraries -> open a PR please.
    classpath = files()

    excludeFilter = file("${project.rootDir}/code_quality/findbugs.xml")
}


plugins.apply('checkstyle')

check.dependsOn 'checkstyle'

checkstyle {
    toolVersion = '7.3'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/code_quality/checkstyle.xml")

    ignoreFailures false // Fail early.
    showViolations true

    source 'src'
    include '**/*.java'

    classpath = files()
}